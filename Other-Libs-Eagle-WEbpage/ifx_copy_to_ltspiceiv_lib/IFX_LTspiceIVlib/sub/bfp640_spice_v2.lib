***************************************************************												
*	Infineon	Technologies	AG									
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX				
*	VALID	UP	TO	10	GHZ							
*	>>>	BFP640	<<<								
*	(C)	2011	Infineon	Technologies	AG							
*	Version 2.0	December	2011								
***************************************************************					
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC 
*   simulation results. 
* - TEMP is calculated by TEMP=TA+P*(RthJS+RthSA). The junction 
*   temperature TEMP is the sum of the ambient temperature TA and 
*   the temperature increment caused by the dissipated power 
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage). 
* - RthJS is the thermal resistance between the junction and the 
*   soldering point. RthJS for this device is 300 K/W. RthSA is the 
*   thermal resistance of the PCB, from the soldering point to the 
*   ambient. For determination of RthSA please refer to Infineon's 
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
****************************************************************						
*BFP640 C B E1 E2
.SUBCKT BFP640 1 2 3 4

CBEPAR 22 33 4.06693E-013
CBCPAR 22 11 9.476E-014
CCEPAR 11 33 3.623E-014
LB    22 20 2.278E-011
LE   33 30 1.15731E-010
LC   11 10  3.18064E-010
CBEPCK 20 30  3.29284E-014
CBCPCK 20 10  7.41999E-015
CCEPCK 10 30  7.95208E-015
LBX    20 2 8.37988E-010
LEX   30 35 1.84227E-010
LCX   10 1  3.84823E-010

RSub 55 30 160
RE1 35 3 1E-03
RE2 35 4 1E-03

Q1 11 22 33 55 M_BFP640


.MODEL 	M_BFP640	NPN(										
+	IS	=	5.55E-015									
+	BF	=	571.059									
+	NF	=	1.05487									
+	VAF	=	311.9									
+	IKF	=	0.121064									
+	ISE	=	2.154E-012									
+	NE	=	3.841									
+	BR	=	113.5		
+	NR	=	1.019		
+	VAR	=	1.455	
+	IKR	=	0.01298		
+	ISC	=	3.503E-018		
+	NC	=	1.146		
+	RB	=	8		
+	IRB	=	0		
+	RBM	=	3.5	
+	RE	=	0.117098		
+	RC	=	14.3227	
+	XTB	=	-2		
+	EG	=	1.11		
+	XTI	=	0.15		
+	CJE	=	6.116E-014		
+	VJE	=	1.341		
+	MJE	=	0.852		
+	TF	=	2.1E-012		
+	XTF	=	25		
+	VTF	=	26.68		
+	ITF	=	2.7		
+	PTF	=	0.2091		
+	CJC	=	5.168E-014		
+	VJC	=	0.881		
+	MJC	=	1.245		
+	XCJC	=	0.7499		
+	TR	=	1.24669E-005
+	CJS	=	4.34E-014		
+	MJS	=	0.2175
+   VJS =   0.5814 		
+	FC	=	0.5		
+	KF	=	7.116E-011		
+	AF	=	1.7)			
***************************************************************					
					

.ENDS BFP640
