***************************************************************												
*	Infineon	Technologies	AG									
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX				
*	VALID	UP	TO	10	GHZ							
*	>>>	BFP720FESD	<<<								
*	(C)	2010	Infineon Technologies AG	
*	Version 2.0	June	2010						
***************************************************************		
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC 
*   simulation results. 
* - TEMP is calculated by TEMP=TA+P*(RthJS+RthSA). The junction 
*   temperature TEMP is the sum of the ambient temperature TA and 
*   the increment of temperature caused by the dissipated power 
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage). 
* - RthJS is the thermal resistance between the junction and the 
*   soldering point. RthJS for this device is 405 K/W. RthSA is the 
*   thermal resistance of the PCB, from the soldering point to the 
*   ambient. For determination of RthSA please refer to Infineon's 
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
****************************************************************			
*BFP720FESD C B E1 E2
.SUBCKT BFP720FESD 1 2 3 4


CBEPAR 22 33 5.67E-014
CBCPAR 22 11 1.155E-014
CCEPAR 11 33 7.37E-014
LB    22 20 5.317E-010
LE   33 30 6.394E-011
LC   11 10  5.834E-010
CBEPCK 20 30  4.24E-014
CBCPCK 20 10  2.798E-015
CCEPCK 10 30  2.013E-014
LBX    20 2 2.507E-014
LEX   30 35 5.897E-011
LCX   10 1  5.256E-011
D1 33 25 M_D1  
D2 55 25  M_D2

RBLfdb 22 25 3.2
RPS 33 55 0.4524
RSUB 30 55 0.3417

RE1 35 3 1E-03
RE2 35 4 1E-03

D3 55 15 M_D3
D4 23 33 M_D4
D5 23 15 M_D5
RLDNBL 15 11 14.8


Q1 11 22 33 55 M_BFP720FESD


.MODEL M_D1 D(
+ IS=3.5E-015	
+ N=1.02
+ RS=6.1
+ CJO=4E-014)


.MODEL M_D2 D(
+ IS=800E-018	
+ N=1.02
+ RS=4170
+ CJO=4.5E-014)


.MODEL M_D3 D(
+ IS=800E-018	
+ N=1.02
+ RS=1380
+ CJO =9.5E-014)


.MODEL M_D4 D(
+ IS=3.5E-015	
+ N=1.02
+ RS=0.2
+ CJO =3E-014)

.MODEL M_D5 D(
+ IS=3.5E-015	
+ N=1.02
+ RS=4.7
+ CJO =3E-014)


.MODEL 	M_BFP720FESD	NPN(										
+	IS	=	5.986E-016									
+	BF	=	713.3									
+	NF	=	1.01									
+	VAF	=	83.55									
+	IKF	=	0.08027									
+	ISE	=	2.195E-014									
+	NE	=	2									
+	BR	=	139.8		
+	NR	=	1.019		
+	VAR	=	1.45	
+	IKR	=	0.005132		
+	ISC	=	1.879E-015		
+	NC	=	1.38		
+	RB	=	2.154		
+	IRB	=	0.02754		
+	RBM	=	0.5522	
+	RE	=	0.04718		
+	RC	=	7.14	
+	XTB	=	-1.7		
+	EG	=	1.11		
+	XTI	=	0.1		
+	CJE	=	2.166E-013		
+	VJE	=	0.5256		
+	MJE	=	0.1269		
+	TF	=	1.83E-012		
+	XTF	=	12.25		
+	VTF	=	4.615		
+	ITF	=	0.9963		
+	PTF	=	0.1		
+	CJC	=	6.91E-014		
+	VJC	=	0.5571		
+	MJC	=	0.2651		
+	XCJC	=	0.7499		
+	TR	=	8.888E-011
+	CJS	=	3.007E-013		
+	MJS	=	0.209
+   VJS =   0.7053 		
+	FC	=	0.5		
+	KF	=	3.3E-12		
+	AF	=	1.4)
***************************************************************					
					

.ENDS BFP720FESD
