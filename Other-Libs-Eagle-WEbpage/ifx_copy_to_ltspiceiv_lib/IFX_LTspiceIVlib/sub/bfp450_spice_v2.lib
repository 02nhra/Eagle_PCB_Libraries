***************************************************************												
*	Infineon	Technologies	AG									
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX				
*	VALID	UP	TO	10	GHZ							
*	>>>	BFP450	<<<								
*	(C)	2012	Infineon	Technologies	AG							
*	Version 2.0	January	2012								
***************************************************************
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC 
*   simulation results. 
* - TEMP is calculated by TEMP=TA+P*(RthJS+RthSA). The junction 
*   temperature TEMP is the sum of the ambient temperature TA and 
*   the increment of temperature caused by the dissipated power 
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage). 
* - RthJS is the thermal resistance between the junction and the 
*   soldering point. RthJS for this device is 120 K/W. RthSA is the 
*   thermal resistance of the PCB, from the soldering point to the 
*   ambient. For determination of RthSA please refer to Infineon's 
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
****************************************************************			
*BFP450 C B E1 E2 
.SUBCKT BFP450 1 2 3 4

CBEPAR 22 33 1.201E-013
CBCPAR 22 11 3.024E-013
CCEPAR 11 33 3.697E-017
LB    22 20 9.042E-010
LE   33 30 2.033E-010
LC   11 10  2.125E-010
CBEPCK 20 30  1.106E-014
CBCPCK 20 10  2.515E-014
CCEPCK 10 30  2.247E-013
LBX    20 2 1.184E-012
LEX   30 35 2.311E-013
LCX   10 1  7.742E-010

RE1 35 3 1E-03
RE2 35 4 1E-03
RSub 55 33 260

Diode_sub 55 11 M_Diode_sub

Q1 11 22 33 55 M_BFP450

.MODEL M_Diode_sub D(
+	IS	=	1.131E-014				
+	N	=	1.02			
+	RS	=	0.2			
+	CJO	=	4E-014)			

.MODEL 	M_BFP450  	NPN(										
+	IS	=	1.21E-016									
+	BF	=	112.3									
+	NF	=	1									
+	VAF	=	46.19									
+	IKF	=	1.005									
+	ISE	=	2.427E-014									
+	NE	=	2									
+	BR	=	18.84		
+	NR	=	0.9966		
+	VAR	=	3.604	
+	IKR	=	0.03848		
+	ISC	=	1.099E-014		
+	NC	=	2		
+	RB	=	3.193		
+	IRB	=	0		
+	RBM	=	2.725	
+	RE	=	0.09391		
+	RC	=	1.578	
+	XTB	=	0.04525		
+	EG	=	1.12		
+	XTI	=	4.267		
+	CJE	=	1.777E-012		
+	VJE	=	0.7811		
+	MJE	=	0.39		
+	TF	=	4.125E-012		
+	XTF	=	8.008		
+	VTF	=	4.189		
+	ITF	=	1.889		
+	PTF	=	0.1		
+	CJC	=	5.069E-013		
+	VJC	=	0.6403		
+	MJC	=	0.31		
+	XCJC	=	0.7031		
+	TR	=	4.163E-009
+	CJS	=	1.664E-012		
+	MJS	=	0.1547			
+	VJS	=	0.2526 		
+	FC	=	0.5		
+	KF	=	5.5e-12
+	AF	=	1.99)			
***************************************************************					
				
.ENDS BFP450
